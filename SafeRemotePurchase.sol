// SPDX-License-Identifier: GPL-3.0
pragma solidity ^0.8.4;
contract Purchase {
    uint public value;
    address payable public seller;
    address payable public buyer;
    mapping(address => uint256) public lastCalls;

    enum State { Created, Locked, Release, Inactive }
    // The state variable has a default value of the first member, `State.created`
    State public state;

    modifier condition(bool condition_) {
        require(condition_);
        _;
    }

    /// Only the buyer can call this function.
    error OnlyBuyer();
    /// Only the seller can call this function.
    error OnlySeller();
    /// The function cannot be called at the current state.
    error InvalidState();
    /// The provided value has to be even.
    error ValueNotEven();

    // confirmPurchase 
    modifier fiveMin(address caller_) {
    require(msg.sender == buyer || block.timestamp - lastCalls[buyer] >= 300, 'Need to wait 5 minutes'); // 300 seconds is 5 minutes can replace with 5 minutes
    // make state unlocked, checks should be done before mods
    // check state, after all the things are mapping
    _;
    }

    // old buyer modifier
    // modifier onlyBuyer() {
    //     if (msg.sender != buyer)
    //         revert OnlyBuyer();
    //     _;
    // }

    // new buyer modifier
      modifier onlyBuyer() {
        require(msg.sender == buyer, 'You are not the buyer');
        _;
    }

    modifier onlySeller() {
        if (msg.sender != seller)
            revert OnlySeller();
        _;
    }

    modifier inState(State state_) {
        if (state != state_)
            revert InvalidState();
        _;
    }

    event Aborted();
    event PurchaseConfirmed();
    event ItemReceived();
    event SellerRefunded();

    // Ensure that `msg.value` is an even number.
    // Division will truncate if it is an odd number.
    // Check via multiplication that it wasn't an odd number.
    constructor() payable {
        seller = payable(msg.sender);
        value = msg.value / 2;
        if ((2 * value) != msg.value)
            revert ValueNotEven();
    }

    /// Abort the purchase and reclaim the ether.
    /// Can only be called by the seller before
    /// the contract is locked.
    function abort()
        external
        onlySeller
        inState(State.Created)
    {
        emit Aborted();
        state = State.Inactive;
        // We use transfer here directly. It is
        // reentrancy-safe, because it is the
        // last call in this function and we
        // already changed the state.
        seller.transfer(address(this).balance);
    }

    /// Confirm the purchase as buyer.
    /// Transaction has to include `2 * value` ether.
    /// The ether will be locked until confirmReceived
    /// is called.
    function confirmPurchase()
        external
        inState(State.Created)
        condition(msg.value == (2 * value))
        payable
    {
        emit PurchaseConfirmed();
        state = State.Locked;
        buyer = payable(msg.sender);
        lastCalls[msg.sender] = block.timestamp;
    }

    /// Confirm that you (the buyer) received the item.
    /// This will release the locked ether.
    function confirmReceived()
        external
        onlyBuyer
        inState(State.Locked)
    {
        emit ItemReceived();
        // It is important to change the state first because
        // otherwise, the contracts called using `send` below
        // can call in again here.
        state = State.Release;
        buyer.transfer(value);
    }

    /// This function refunds the seller, i.e.
    /// pays back the locked funds of the seller.
    function refundSeller()
        external
        onlySeller
        inState(State.Release)
    {
        emit SellerRefunded();
        // It is important to change the state first because
        // otherwise, the contracts called using `send` below
        // can call in again here.
        state = State.Inactive;
        seller.transfer(3 * value);
    }
  
    /// This function combines confirmReceived and refundSeller
    /// Combines into a function called completePurchase
    /// When current state is locked AND
    /// Either: The called is the buyer OR >5 min elapsed since buyer called comfirmPurchase
    function completePurchase()
        external
        fiveMin(msg.sender)
        inState(State.Locked)
    {
        emit ItemReceived();
        emit SellerRefunded();
        // It is important to change the state first because
        // otherwise, the contracts called using `send` below
        // can call in again here.
        state = State.Inactive;

        buyer.transfer(value);
        seller.transfer(3 * value);
    }
}
